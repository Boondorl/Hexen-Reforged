class ArmorBox : TooltipMenuElement
{
	enum EArmorTypes
	{
		AT_HEAD = 1,
		AT_AMUL,
		AT_SHIELD,
		AT_CHEST,
		AT_BRACE
	}
	
	Inventory armor;
	int type;
	
	override void Initialize()
	{
		super.Initialize();
		
		background = TexMan.CheckForTexture("ARTIBOX", TexMan.Type_Any);
		bNavigate = true;
	}
	
	override Vector2, Vector2, Vector2 GetNavCollisionBox(int dir, bool inverted)
	{
		Vector2 pos = center;
		Vector2 origin, s;
		Vector2 half = size / 2;
		
		switch (dir)
		{
			case MD_LEFT:
				if (inverted)
				{
					pos = (1920, center.y);
					origin.x = center.x + half.x;
					origin.y = center.y - half.y;
					s.x = 1920 - center.x + half.x;
					s.y = size.y;
				}
				else
				{
					origin.y = center.y - half.y;
					s.x = center.x - half.x;
					s.y = size.y;
				}
				break;
				
			case MD_UP:
				if (inverted)
				{
					pos = (center.x, 1080);
					origin.x = center.x - half.x;
					origin.y = center.y + half.y;
					s.x = size.x;
					s.y = 1080 - (center.y + half.y);
				}
				else
				{
					origin.x = center.x - half.x;
					s.x = size.x;
					s.y = center.y - half.y;
				}
				break;
				
			case MD_RIGHT:
				if (inverted)
				{
					pos = (0, center.y);
					origin.y = center.y - half.y;
					s.x = center.x - half.x;
					s.y = size.y;
				}
				else
				{
					origin.x = center.x + half.x;
					origin.y = center.y - half.y;
					s.x = 1920 - center.x + half.x;
					s.y = size.y;
				}
				break;
				
			case MD_DOWN:
				if (inverted)
				{
					pos = (center.x, 0);
					origin.x = center.x - half.x;
					s.x = size.x;
					s.y = center.y - half.y;
				}
				else
				{
					origin.x = center.x - size.x*max(1, type-2);
					origin.y = center.y + half.y;
					s.x = size.x*2*max(1, type-2);
					s.y = 1080 - (center.y + half.y);
				}
				break;
		}
		
		return pos, origin, s;
	}
	
	private void DrawArmorStats()
	{
		if (!armor)
			return;
		
		if (!tool)
			tool = Tooltip.Create("Tooltip", handler, (center.x, center.y+size.y/2), 600, self);
		
		if (tool)
		{
			tool.text = String.Format("%s%s:%s %d / %d\n\n", Font.TEXTCOLOR_BOLD, Stringtable.Localize("$TITLE_DURA"), Font.TEXTCOLOR_NORMAL, armor.health, armor.default.health);
			let ar = ArmorPiece(armor);
			if (ar)
				tool.text.AppendFormat("%s", ar.GetDescription());
		}
	}
	
	override void OnSelected()
	{
		DrawArmorStats();
	}
	
	override void PreChildTick()
	{
		if (!armor)
		{
			if (tool)
				tool.Destroy();
			
			switch (type)
			{
				case AT_HEAD:
					armor = players[consoleplayer].mo.FindInventory("HelmetPiece");
					break;
					
				case AT_AMUL:
					armor = players[consoleplayer].mo.FindInventory("AmuletPiece");
					break;
					
				case AT_CHEST:
					armor = players[consoleplayer].mo.FindInventory("ChestPiece");
					break;
					
				case AT_BRACE:
					armor = players[consoleplayer].mo.FindInventory("BracerPiece");
					break;
					
				case AT_SHIELD:
					armor = players[consoleplayer].mo.FindInventory("ShieldPiece");
					break;
			}
		}
		else if (armor.owner != players[consoleplayer].mo)
			armor = null;
	}
	
	override void PreChildExecute(double fracTic)
	{
		if (armor)
		{
			double ratio = (1 - armor.health*1. / armor.default.health) * 0.6;
			int alpha = int(0xFF * ratio) << 24;
			int col = alpha | 0x00FF0000;
			
			DrawTexture(armor.icon, scaledCenter, (2.25,2.25), color: col);
		}
	}
	
	override void OnHover()
	{
		DrawArmorStats();
	}
}