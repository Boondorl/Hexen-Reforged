class ReforgedBeast : RPGMonster replaces Demon2
{
	private int flameDuration;
	private int meleeCoolDown; // For flamethrower
	
	Default
	{
		RPGMonster.JumpSpeed 6;
		RPGMonster.XP 100;
		Health 250;
		Radius 32;
		Height 74;
		DeathHeight 37;
		Mass 200;
		Damage 75;
		Speed 3;
		MeleeRange 128;
		Painchance 100;
		DamageFactor "Fire", 0.75;
		DamageFactor "Burn", 0.75;
		SeeSound "DEM2t/sight";
		AttackSound "DEM2t/attack";
		PainSound "DEM2t/pain";
		DeathSound "DEM2t/death";
		ActiveSound "DEM2t/active";
		Obituary "$OB_DEM2T";
		Tag "$FN_DEM2T";
		
		+RPGMONSTER.JUMPER
		-NOICEDEATH
	}
	
	States
	{
		Spawn:
			DEM2 AABB 5 A_MonsterLook;
			Loop;
			
		See:
			DEM2 ABCDEF 4 A_MonsterChase;
			Loop;
			
		Melee:
			TNT1 A 0 {flameDuration = TICRATE;}
			DEM2 H 10 A_MonsterFaceTarget;
			TNT1 A 0 {meleeCoolDown = 70;}
			DEM2 I 2 A_MonsterProjectile("DEM2tFlame", 48);
			TNT1 A 0
			{
				if (!target || target.health <= 0 || --flameDuration <= 0 || Distance3D(target)-target.radius > MeleeRange*2)
					SetState(SeeState);
			}
			Goto Melee+2;
			
		Missile:
			DEM2 H 10 A_MonsterFaceTarget;
			DEM2 I 10 A_MonsterProjectile("RDEM2tBall", 48);
			Goto See;
			
		Stumble:
			TNT1 A 0 A_Stumble;
			DEM2 G 18 A_Pain;
			Goto See;
			
		Shock:
			DEM2 G 9 A_StartSound(PainSound, CHAN_VOICE);
			DEM2 G 9;
			DEM2 G 9 A_StartSound(PainSound, CHAN_VOICE);
			DEM2 G 9;
			DEM2 G 9 A_StartSound(PainSound, CHAN_VOICE);
			DEM2 G 9;
			DEM2 G 9 A_StartSound(PainSound, CHAN_VOICE);
			DEM2 G 9;
			Goto See;
			
		Pain:
			DEM2 G 3;
			DEM2 G 3 A_Pain;
			Goto See;
			
		Death:
			DEM2 R 6;
			DEM2 S 6 A_Scream;
			DEM2 TUV 6;
			DEM2 W 6 A_NoBlocking;
			DEM2 XY 6;
			DEM2 Z -1;
			Stop;
			
		XDeath:
			DEM2 J 5;
			DEM2 K 6 A_Scream;
			DEM2 L 5;
			DEM2 M 6;
			DEM2 N 5;
			DEM2 O 6 A_NoBlocking;
			DEM2 P 5;
			DEM2 Q -1;
			Stop;
			
		Raise:
			DEM2 YXWVUTSR 6;
			Goto See;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		
		CreateBox("DEM2tBody");
		CreateBox("DEM2tShoulder");
		CreateBox("DEM2tHead");
	}
	
	override void InitializeAilments()
	{
		SetAilment(0, 'Shock');
		SetAilment(1, 'Chill');
		SetAilment(2, 'Poison');
		SetAilment(3, 'Stumble');
		SetAilment(4, 'Flinch');
		SetAilment(5, 'Bleed');
	}
	
	override bool CheckMelee()
	{
		if (target && meleeCoolDown <= 0 && CheckMelee3D())
		{
			let tracer = SightTracer(new("SightTracer"));
			if (tracer)
			{
				tracer.bMissile = true;
				tracer.master = self;
				tracer.target = target;
				tracer.bThruObj = bAttackThruObj;
				tracer.bThruMonst = bAttackThruMonst;
				
				double h = missileCheckHeight ? missileCheckHeight : height/2 + 4;
				h -= floorclip;
				Vector3 start = pos + (0,0,h);
				Vector3 dir = Vec3To(target) + (0,0,(target.height/2-target.floorclip) - h);
				
				tracer.Trace(start, CurSector, dir.Unit(), dir.Length(), 0);
				if (tracer.results.hitType == TRACE_HitNone || tracer.results.hitActor == target)
					return true;
			}
		}
		
		return false;
	}
	
	override void Tick()
	{
		if (!isFrozen())
		{
			if (meleeCoolDown > 0)
				--meleeCoolDown;
		}
		
		super.Tick();
	}
}

class DEM2tBody : CollisionBox
{
	Default
	{
		Radius 32;
		Height 54;
	}
}

class DEM2tShoulder : CollisionBox
{
	Default
	{
		CollisionBox.Length 19;
		CollisionBox.ForwardOffset -19;
		CollisionBox.UpOffset 54;
		Radius 32;
		Height 20;
		
		+COLLISIONBOX.ORIENTED
	}
}

class DEM2tHead : CollisionBox
{
	Default
	{
		CollisionBox.Multi 1.2;
		CollisionBox.ForwardOffset 19;
		CollisionBox.UpOffset 54;
		Radius 13;
		Height 20;
		
		+COLLISIONBOX.CRITICALZONE
	}
}

class RDEM2tBall : RPGMissile
{
	Default
	{
		Radius 6;
		Height 12;
		Speed 16;
		Damage 30;
		DamageType "Fire";
		RPGMissile.AilmentType "Burn";
		RPGMissile.AilmentPower 35;
		RPGMissile.AilmentDamage 10;
		RenderStyle "Add";
		SeeSound "DEM2t/attack";
		
		-ACTIVATEIMPACT
		-ACTIVATEPCROSS
		+SPAWNSOUNDSOURCE
		+ZDOOMTRANS
	}
	
	States
	{
		Spawn:
			FRB1 AABBCC 2 Bright A_SpawnItemEx("Puffy", random2[DEM2tPuff]()*0.015625, random2[DEM2tPuff]()*0.015625, random2[DEM2tPuff]()*0.015625, 
												0,0,0,0,SXF_ABSOLUTEPOSITION, 64);
			Loop;
			
		Death:
			FRB1 D 4 Bright
			{
				for (int i = 0; i < 8; ++i)
				{
					let ball = Spawn("DEM2tBallSmall", pos, ALLOW_REPLACE);
					if (ball)
					{
						ball.target = target;
						ball.angle = angle + i*45;
						ball.Vel3DFromAngle(ball.speed, ball.angle, -45);
					}
				}
			}
			FRB1 EFGH 4 Bright;
			Stop;
	}
}

class DEM2tBallSmall : RPGMissile
{
	Default
	{
		Radius 2;
		Height 4;
		Speed 8;
		Scale 0.5;
		Damage 5;
		DamageType "Fire";
		RenderStyle "Add";
		
		-ACTIVATEIMPACT
		-ACTIVATEPCROSS
		-NOGRAVITY
		+ZDOOMTRANS
	}
	
	States
	{
		Spawn:
			FRB1 AABBCC 2 Bright;
			Loop;
			
		Death:
			FRB1 DEFGH 4 Bright;
			Stop;
	}
}

class DEM2tFlame : RPGMissile
{
	Default
	{
		Radius 2;
		Height 4;
		Speed 16;
		Damage 4;
		Threshold 8;
		Scale 0.5;
		DamageType "Fire";
		RPGMissile.AilmentType "Burn";
		RPGMissile.AilmentPower 10;
		RPGMissile.AilmentDamage 4;
		RenderStyle "Add";
		Obituary "$OB_MPPPHOENIXROD";
		
		+ZDOOMTRANS
		+RPGMISSILE.PENETRATING
		+RPGMISSILE.SHIELDBUSTER
		+RPGMISSILE.NOCRIT
	}

	States
	{
		Spawn:
			FX09 ABA 2 BRIGHT;
			FX09 B 2 BRIGHT;
			FX09 CDEF 2 BRIGHT;
			Stop;
			
		Death:
			FX09 G 3 BRIGHT;
			FX09 H 3 BRIGHT A_FloatPuff;
			FX09 I 4 BRIGHT;
			FX09 JK 5 BRIGHT;
			Stop;
	}
	
	override void Tick()
	{
		if (bMissile && radius < threshold && !isFrozen())
		{
			A_SetScale(scale.x + 0.25);
			A_SetSize(radius+1, height+2);
		}
		
		super.Tick();
	}

	void A_FloatPuff()
	{
		Vel.Z += 1.8;
	}
}