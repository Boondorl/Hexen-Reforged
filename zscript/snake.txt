class ReforgedSnake : RPGMonster replaces CentaurLeader
{
	Default
	{
		RPGMonster.XP 120;
		RPGMonster.ArmorType AR_CHEST|AR_BRACE;
		Health 280;
		Radius 22;
		Height 70;
		DeathHeight 35;
		Speed 4;
		Damage 35;
		Painchance 48;
		MeleeRange 64;
		AttackSound "snake/attack";
		SeeSound "snake/sight";
		PainSound "snake/pain";
		DeathSound "snake/death";
		ActiveSound "snake/active";
		Obituary "$OB_SNAKE";
		Tag "$FN_SNAKE";
		
		+RPGMONSTER.BLOCKER
	}
	
	States
	{
		Spawn:
			SNKE AABB 5 A_MonsterLook;
			Loop;
			
		See:
			SNKE ABCD 4 A_MonsterChase;
			Loop;
			
		/*Melee:
			SNKE FF 6 A_MonsterFaceTarget;
			SNKE F 6; A_ReforgedComboAttack("RSnakeProjB", 32, 15);
			Goto See;*/
			
		Missile:
			SNKE FF 5 A_MonsterFaceTarget;
			SNKE FFF 4 A_MonsterProjectile("DummySnakeMissile");
			SNKE FFF 5 A_MonsterFaceTarget;
			SNKE F 4 A_MonsterProjectile("RSnakeProjB");
			Goto See;
			
		Shock:
			SNKE E 9 A_StartSound(PainSound, CHAN_VOICE);
			SNKE E 9;
			SNKE E 9 A_StartSound(PainSound, CHAN_VOICE);
			SNKE E 9;
			SNKE E 9 A_StartSound(PainSound, CHAN_VOICE);
			SNKE E 9;
			SNKE E 9 A_StartSound(PainSound, CHAN_VOICE);
			SNKE E 9;
			Goto See;
			
		Pain:
			SNKE E 3;
			SNKE E 3 A_Pain;
			Goto See;
			
		Death:
			SNKE G 5;
			SNKE H 5 A_Scream;
			SNKE IJKL 5;
			SNKE M 5 A_NoBlocking;
			SNKE NO 5;
			SNKE P -1;
			Stop;
			
		Raise:
			SNKE ONMLKJIHG 5;
			Goto See;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		
		CreateBox("SnakeBody");
		CreateBox("SnakeHead");
	}
	
	override void InitializeAilments()
	{
		SetAilment(0, 'Burn');
		SetAilment(1, 'Shock');
		SetAilment(2, 'Chill');
		SetAilment(3, 'Poison');
		SetAilment(4, 'Flinch');
		SetAilment(5, 'Bleed');
	}
}

class SnakeBody : CollisionBox
{
	Default
	{
		Radius 22;
		Height 50;
	}
}

class SnakeHead : CollisionBox
{
	Default
	{
		CollisionBox.Multi 1.5;
		CollisionBox.UpOffset 50;
		CollisionBox.ForwardOffset 14;
		Radius 8;
		Height 12;
		
		+COLLISIONBOX.CRITICALZONE
	}
}

class DummySnakeMissile : DummyMissile
{
	Default
	{
		Speed 14;
	}
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		let orb = Spawn("SnakeProjOrbiter", pos, ALLOW_REPLACE);
		if (orb)
		{
			children.Push(orb);
			orb.target = target;
			orb.master = self;
			orb.vel = vel;
			orb.angle = angle;
			orb.pitch = -VectorAngle(vel.xy.Length(), vel.z);
		}
	}
}

class SnakeProjOrbiter : RPGMissile
{
	private double offsetDist;
	private double pchOfs;
	
	Default
	{
		Radius 3;
		Height 6;
		Damage 10;
		DamageType "Electric";
		RPGMissile.AilmentType "Shock";
		RPGMissile.AilmentPower 35;
		RenderStyle "Add";
		SeeSound "snake/attack";
		
		-ACTIVATEIMPACT
		-ACTIVATEPCROSS
		+SPAWNSOUNDSOURCE
		+ZDOOMTRANS
		+FORCEXYBILLBOARD
	}
	
	States
	{
		Spawn:
			SNFX AAAAABBBBBCCCCCDDDDD 1 Bright A_TrackSnake;
			Loop;
			
		Death:
			SNFX EF 5 Bright;
			SNFX G 4 Bright;
			SNFX HI 3 Bright;
			Stop;
	}
	
	void A_TrackSnake()
	{
		if (master)
		{
			if (offsetDist < 12)
				++offsetDist;
			
			Vector3 up = (0,0,-sin(pitch - 90));
			up.xy = AngleToVector(angle, cos(pitch - 90));
			up *= sin(pchOfs)*offsetDist;
			
			Vector2 xyOffset = AngleToVector(angle-90, cos(pchOfs)*offsetDist);
			
			SetOrigin(master.pos + up + (xyOffset.x, xyOffset.y, 0), true);
					
			pchOfs += 15;
		}
	}
}

class RSnakeProjB : RPGMissile
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 24;
		Damage 25;
		DamageType "Fire";
		RPGMissile.AilmentType "Burn";
		RPGMissile.AilmentPower 50;
		RPGMissile.AilmentDamage 10;
		RenderStyle "Add";
		SeeSound "snake/attack";
		
		-ACTIVATEIMPACT
		-ACTIVATEPCROSS
		+SPAWNSOUNDSOURCE
		+ZDOOMTRANS
		+FORCEXYBILLBOARD
	}
	
	States
	{
		Spawn:
			SNFX JK 6 Bright;
			Loop;
			
		Death:
			SNFX LM 5 Bright;
			SNFX N 4 Bright;
			SNFX O 3 Bright;
			Stop;
	}
}