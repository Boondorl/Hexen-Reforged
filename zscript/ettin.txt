class EttinBlock : BlockBox
{
	Default
	{
		Radius 12;
		Height 24;
		BlockBox.Width 4;
		BlockBox.Height 24;
		BlockBox.Length 4;
		BlockBox.BlockSound "FighterHammerHitWall";
		BlockBox.BlockReduction 0.3;
		BlockBox.ParryReduction 0.5;
		BlockBox.ParryWindow 4;
		BlockBox.ForwardOffset 36;
		BlockBox.UpOffset 45;
		BlockBox.Roll -80;
	}
}

class ReforgedEttin : RPGMonster
{
	Default
	{
		RPGMonster.XP 40;
		RPGMonster.ArmorType AR_CHEST;
		RPGMonster.ShieldType "EttinBlock";
		Health 175;
		Radius 25;
		Height 68;
		DeathHeight 34;
		Mass 175;
		Speed 3;
		Damage 60;
		AttackSound "FighterHammerMiss";
		SeeSound "EttinSight";
		PainSound "EttinPain";
		DeathSound "EttinDeath";
		ActiveSound "EttinActive";
		HowlSound "PuppyBeat";
		Obituary "$OB_ETTIN";
		Tag "$FN_ETTIN";
		
		+RPGMONSTER.BLOCKER
		+RPGMONSTER.JUMPER
	}
	
	States
	{
		Spawn:
			ETTN A 10 A_MonsterLook;
			Loop;
			
		See:
			ETTN ABCD 5 A_MonsterChase;
			Loop;
			
		Block:
			ETTN F 2 A_MonsterFaceTarget;
			TNT1 A 0 A_MonsterBlock;
			ETTN FFFFFFF 5 A_MonsterFaceTarget;
		Unblock:
			TNT1 A 0 A_MonsterUnblock;
			ETTN F 2 A_MonsterFaceTarget;
			Goto See;
			
		Parry:
			ETTN H 35 A_Pain;
			Goto See;
			
		Stumble:
			TNT1 A 0 A_Stumble;
			ETTN H 18 A_Pain;
			Goto See;
			
		Shock:
			ETTN H 9 A_StartSound(HowlSound, CHAN_VOICE);
			ETTN H 9;
			ETTN H 9 A_StartSound(HowlSound, CHAN_VOICE);
			ETTN H 9;
			ETTN H 9 A_StartSound(HowlSound, CHAN_VOICE);
			ETTN H 9;
			ETTN H 9 A_StartSound(HowlSound, CHAN_VOICE);
			ETTN H 9;
			Goto See;
			
		Pain:
			ETTN H 7 A_Pain;
			Goto See;
			
		Melee:
			ETTN EF 6 A_MonsterFaceTarget;
			ETTN G 8 A_MonsterMelee("MaceBox", 20, 11, 5, 32, 10, -20, 15, -15, 20, 20);
			Goto See;
			
		Death:
			ETTN IJ 4;
			ETTN K 4 A_Scream;
			ETTN L 4 A_NoBlocking;
			ETTN M 4 A_QueueCorpse;
			ETTN NOP 4;
			ETTN Q -1;
			Stop;
			
		XDeath:		
			ETTB A 4;
			ETTB B 4 A_NoBlocking;
			ETTB C 4 A_SpawnItemEx("EttinMace", 0,0,8.5, 
									random[DropMace](-128,127) * 0.03125,
									random[DropMace](-128,127) * 0.03125,
									10 + random[DropMace](0,255) * 0.015625, 0, SXF_ABSOLUTEVELOCITY);
			ETTB D 4 A_Scream;
			ETTB E 4 A_QueueCorpse;
			ETTB FGHIJK 4;
			ETTB L -1;
			Stop;
			
		Ice:
			ETTN R 5 A_FreezeDeath;
			ETTN R 1 A_FreezeDeathChunks;
			Wait;
			
		Raise:
			ETTN PONMLKJI 4;
			Goto See;
			
		XRaise:		
			ETTB KJIHGFEDCBA 4;
			Goto See;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		
		CreateBox("EttinBody");
		CreateBox("EttinShoulder");
		CreateBox("EttinHead");
	}
}

class EttinBody : CollisionBox
{
	Default
	{
		Radius 25;
		Height 48;
	}
}

class EttinShoulder : CollisionBox
{
	Default
	{
		CollisionBox.Length 12;
		CollisionBox.ForwardOffset -12;
		CollisionBox.UpOffset 48;
		Radius 25;
		Height 18;
		
		+COLLISIONBOX.ORIENTED
	}
}

class EttinHead : CollisionBox
{
	Default
	{
		CollisionBox.Multi 1.5;
		CollisionBox.ForwardOffset 12;
		CollisionBox.UpOffset 48;
		Radius 13;
		Height 18;
		
		+COLLISIONBOX.CRITICALZONE
	}
}