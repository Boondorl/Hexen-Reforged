class CentaurBlock : BlockBox
{
	Default
	{
		Radius 16;
		Height 32;
		BlockBox.Width 16;
		BlockBox.Height 26;
		BlockBox.Length 6;
		BlockBox.BlockSound "FighterHammerHitWall";
		BlockBox.BlockReduction 0.5;
		BlockBox.ParryReduction 0.7;
		BlockBox.ParryWindow 4;
		BlockBox.ForwardOffset 28;
		BlockBox.UpOffset 36;
	}
}

class CentaurSwordBox : HurtBox
{
	Default
	{
		Radius 3;
		Height 2;
		DamageType "Cleave";
		HurtBox.AilmentPower 35;
		HurtBox.AilmentType "Bleed";
		HurtBox.AilmentDamage 10;
		HurtBox.ForwardOffset 16;
		ActiveSound "FighterHammerHitWall";
		AttackSound "FighterAxeHitThing";
	}
}

// Thank god
class ReforgedCentaur : RPGMonster replaces Centaur
{
	Default
	{
		RPGMonster.JumpSpeed 5;
		RPGMonster.XP 70;
		RPGMonster.ArmorType AR_CHEST|AR_HELM|AR_SHIELD|AR_BRACE;
		RPGMonster.ShieldType "CentaurBlock";
		RPGMonster.BlockChance 0.4;
		RPGMonster.FailBlockCoolDown 5;
		RPGMonster.MinBlockCoolDown 70;
		RPGMonster.MaxBlockCoolDown 105;
		Health 250;
		Painchance 135;
		Speed 3;
		Radius 20;
		Height 64;
		DeathHeight 32;
		Mass 120;
		Damage 50;
		SeeSound "CentaurSight";
		AttackSound "CentaurAttack";
		PainSound "CentaurPain";
		DeathSound "CentaurDeath";
		ActiveSound "CentaurActive";
		HowlSound "PuppyBeat";
		Obituary "$OB_CENTAUR";
		Tag "$FN_CENTAUR";
		
		+RPGMONSTER.BLOCKER
		+RPGMONSTER.JUMPER
	}
	
	States
	{
		Spawn:
			CENT AB 10 A_MonsterLook;
			Loop;
			
		See:
			CENT ABCD 4 A_MonsterChase;
			Loop;
			
		Block:
			CENT E 2 A_MonsterFaceTarget;
			TNT1 A 0 A_MonsterBlock;
			CENT EEEEEEE 5 A_MonsterFaceTarget;
		Unblock:
			TNT1 A 0 A_MonsterUnblock;
			CENT E 2 A_MonsterFaceTarget;
			Goto See;
			
		Parry:
			CENT G 35 A_Pain;
			Goto See;
			
		Stumble:
			TNT1 A 0 A_Stumble;
			CENT G 18 A_Pain;
			Goto See;
			
		Shock:
			CENT G 9 A_StartSound(HowlSound, CHAN_VOICE);
			CENT G 9;
			CENT G 9 A_StartSound(HowlSound, CHAN_VOICE);
			CENT G 9;
			CENT G 9 A_StartSound(HowlSound, CHAN_VOICE);
			CENT G 9;
			CENT G 9 A_StartSound(HowlSound, CHAN_VOICE);
			CENT G 9;
			Goto See;
			
		Pain:
			CENT G 6 A_Pain;
			CENT G 6;
			Goto See;
			
		Melee:
			CENT HI 6 A_MonsterFaceTarget;
			CENT J 8 A_MonsterMelee("CentaurSwordBox", 25, 0, 5, 48, 10, -20, 10, -15, 20, 20);
			Goto See;
			
		Death:
			CENT K 4;
			CENT L 4 A_Scream;
			CENT MN 4;
			CENT O 4 A_NoBlocking;
			CENT PQ 4;
			CENT R 4 A_QueueCorpse;
			CENT S 4;
			CENT T -1;
			Stop;
			
		XDeath:
			CTXD A 4;
			CTXD B 4 A_NoBlocking;
			CTXD C 4
			{
				A_SpawnItemEx("CentaurSword", 0, 0, 45,
									1 + random[CentaurDrop](-128,127)*0.03125,
									1 + random[CentaurDrop](-128,127)*0.03125,
									8 + random[CentaurDrop](0,255)*0.015625, 270);
				A_SpawnItemEx("CentaurShield", 0, 0, 45,
									1 + random[CentaurDrop](-128,127)*0.03125,
									1 + random[CentaurDrop](-128,127)*0.03125,
									8 + random[CentaurDrop](0,255)*0.015625, 90);
			}
			CTXD D 3 A_Scream;
			CTXD E 4 A_QueueCorpse;
			CTXD F 3;
			CTXD G 4;
			CTXD H 3;
			CTXD I 4;
			CTXD J 3;
			CTXD K -1;
			
		Ice:
			CENT U 5 A_FreezeDeath;
			CENT U 1 A_FreezeDeathChunks;
			Wait;
			
		Raise:
			CENT SRQPONMLK 4;
			Goto See;
			
		XRaise:
			CTXD J 3;
			CTXD I 4;
			CTXD H 3;
			CTXD G 4;
			CTXD F 3;
			CTXD E 4;
			CTXD D 3;
			CTXD CBA 4;
			Goto See;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		
		CreateBox("CentaurBody");
		CreateBox("CentaurHead");
	}
}

class ReforgedCentaurLeader : ReforgedCentaur replaces CentaurLeader {}

class CentaurBody : CollisionBox
{
	Default
	{
		Radius 20;
		Height 56;
	}
}

class CentaurHead : CollisionBox
{
	Default
	{
		CollisionBox.Multi 1.5;
		CollisionBox.ForwardOffset 12;
		CollisionBox.UpOffset 56;
		Radius 8;
		Height 8;
		
		+COLLISIONBOX.CRITICALZONE
	}
}