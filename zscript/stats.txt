class Stat : Inventory abstract
{
	ui int currentLevel;
	ui int currentCost;
	
	protected Array<double> bonusValue;
	protected Array<String> bonusText;
	
	private meta string description;
	
	int level;
	int maxLevel;
	int cost;
	int minLevel;
	
	property MinLevel : minLevel;
	property Level : level;
	property MaxLevel : maxLevel;
	property Description : description;
	property Cost : cost;
	
	Default
	{
		Stat.MinLevel 1;
		Stat.Level 1;
		Stat.MaxLevel 6;
		Stat.Cost 1000;
		
		+INVENTORY.UNDROPPABLE
		+NOBLOCKMAP
	}
	
	override void Tick() {}
	
	virtual void LevelUp(int levels, bool start = false)
	{
		level += levels;
		if (start)
			minLevel += levels;
	}
	
	virtual void GiveDefault() {}
	
	clearscope String GetDescription() const
	{
		return Stringtable.Localize(description);
	}
	
	clearscope double, String GetBonus(uint index) const
	{
		double val;
		String bonus = "";
		
		if (index < bonusValue.Size())
			val = bonusValue[index];
		if (index < bonusText.Size())
			bonus = Stringtable.Localize(bonusText[index]);
		
		return val, bonus;
	}
	
	clearscope uint BonusCount() const
	{
		return max(bonusValue.Size(), bonusText.Size());
	}
}

class Agility : Stat
{
	Default
	{
		Tag "$TAG_AGL";
		Stat.Description "$DESC_AGL";
	}
	
	override void LevelUp(int levels, bool start)
	{
		super.LevelUp(levels, start);
		
		owner.player.mo.speed += 0.05*levels;
		let rpg = RPGPlayer(owner.player.mo);
		if (rpg)
		{
			rpg.carryCapacity += 20*levels;
			rpg.coolDownModifier -= 0.1*levels;
		}
	}
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		bonusValue.Push(5);
		bonusText.Push("$BONUS_AGL1");
		bonusValue.Push(2);
		bonusText.Push("$BONUS_AGL2");
		bonusValue.Push(-10);
		bonusText.Push("$BONUS_AGL3");
	}
}

class Vitality : Stat
{
	Default
	{
		Tag "$TAG_VIT";
		Stat.Description "$DESC_VIT";
	}
	
	override void LevelUp(int levels, bool start)
	{
		super.LevelUp(levels, start);
		
		owner.player.mo.maxHealth += 30*levels;
		if (start)
			owner.player.mo.GiveBody(30*levels);
		else if (levels < 0)
		{
			int diff = owner.player.mo.health - owner.player.mo.maxHealth;
			if (diff > 0)
			{
				owner.player.mo.health -= diff;
				owner.player.health = owner.player.mo.health;
			}
		}
	}
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		bonusValue.Push(30);
		bonusText.Push("$BONUS_VIT");
	}
}

class Wisdom : Stat
{
	Default
	{
		Tag "$TAG_WIS";
		Stat.Description "$DESC_WIS";
	}
	
	override void LevelUp(int levels, bool start)
	{
		super.LevelUp(levels, start);
		
		let bmana = Ammo(owner.player.mo.FindInventory("BlueMana"));
		if (bmana)
		{
			bmana.MaxAmount += 30*levels;
			bmana.BackpackMaxAmount += 30*levels;
			if (start)
				bmana.amount += 30*levels;
			else if (levels < 0)
			{
				int diff = bmana.amount - bmana.maxAmount;
				if (diff > 0)
					bmana.amount -= diff;
			}
		}
		
		let gmana = Ammo(owner.player.mo.FindInventory("GreenMana"));
		if (gmana)
		{
			gmana.MaxAmount += 30*levels;
			gmana.BackpackMaxAmount += 30*levels;
			if (start)
				gmana.amount += 30*levels;
			else if (levels < 0)
			{
				int diff = gmana.amount - gmana.maxAmount;
				if (diff > 0)
					gmana.amount -= diff;
			}
		}
		
		let rmana = Ammo(owner.player.mo.FindInventory("RedMana"));
		if (rmana)
		{
			rmana.MaxAmount += 30*levels;
			rmana.BackpackMaxAmount += 30*levels;
			if (levels < 0)
			{
				int diff = rmana.amount - rmana.maxAmount;
				if (diff > 0)
					rmana.amount -= diff;
			}
		}
	}
	
	override void GiveDefault()
	{
		let bmana = Ammo(owner.player.mo.FindInventory("BlueMana"));
		if (bmana)
		{
			bmana.MaxAmount += 30*(level-1);
			bmana.BackpackMaxAmount += 30*(level-1);
			bmana.Amount = bmana.MaxAmount;
		}
		
		let gmana = Ammo(owner.player.mo.FindInventory("GreenMana"));
		if (gmana)
		{
			gmana.MaxAmount += 30*(level-1);
			gmana.BackpackMaxAmount += 30*(level-1);
			gmana.Amount = gmana.MaxAmount;
		}
		
		let rmana = Ammo(owner.player.mo.FindInventory("RedMana"));
		if (rmana)
		{
			rmana.MaxAmount += 30*(level-1);
			rmana.BackpackMaxAmount += 30*(level-1);
		}
	}
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		bonusValue.Push(30);
		bonusText.Push("$BONUS_WIS");
	}
}

class Intelligence : Stat
{
	static const Name bonus[] = {'Fire', 'Ice', 'Electric', 'Poison', 'Burn', 'Illness'};
	
	Default
	{
		Tag "$TAG_INT";
		Stat.Description "$DESC_INT";
	}
	
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if (level <= 1)
			return;
		
		if (!passive)
		{
			bool bonusDmg = false;
			for (int i = 0; i < bonus.Size(); ++i)
			{
				if (damageType == bonus[i])
				{
					bonusDmg = true;
					break;
				}
			}
			
			if (bonusDmg)
				newdamage = round(newdamage * (1 + 0.2*(level-1)));
		}
	}
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		bonusValue.Push(20);
		bonusText.Push("$BONUS_INT");
	}
}

class Strength : Stat
{
	static const Name bonus[] = {'Cleave', 'Pierce', 'Blunt', 'Bleed', 'Melee', 'Hitscan'};
	
	Default
	{
		Tag "$TAG_STR";
		Stat.Description "$DESC_STR";
	}
	
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if (level <= 1)
			return;
		
		if (!passive)
		{
			bool bonusDmg = false;
			for (int i = 0; i < bonus.Size(); ++i)
			{
				if (damageType == bonus[i])
				{
					bonusDmg = true;
					break;
				}
			}
			
			if (bonusDmg)
				newdamage = round(newdamage * (1 + 0.2*(level-1)));
		}
	}
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		bonusValue.Push(20);
		bonusText.Push("$BONUS_STR");
	}
}

class Dexterity : Stat
{
	Default
	{
		Tag "$TAG_DEX";
		Stat.Description "$DESC_DEX";
	}
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		bonusValue.Push(-5);
		bonusText.Push("$BONUS_DEX");
	}
	
	override void LevelUp(int levels, bool start)
	{
		super.LevelUp(levels, start);
		
		let rpg = RPGPlayer(owner.player.mo);
		if (rpg)
			rpg.attackModifier -= 0.05*levels;
	}
}