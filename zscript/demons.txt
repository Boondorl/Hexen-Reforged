class ReforgedSerpent : RPGMonster replaces Demon1
{
	Default
	{
		RPGMonster.XP 100;
		RPGMonster.MissileCheckHeight 62;
		Health 250;
		Speed 15;
		Radius 32;
		Height 80;
		DeathHeight 40;
		Mass 220;
		DamageFactor "Poison", 0.75;
		DamageFactor "Illness", 0.75;
		SeeSound "DemonSight";
		AttackSound "DemonAttack";
		PainSound "DemonPain";
		DeathSound "DemonDeath";
		ActiveSound "DemonActive";
		Obituary "$OB_DEMON1";
		Tag "$FN_DEMON1";
	}
	
	const ChunkFlags = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY;
	
	States
	{
		Spawn:
			DEMN A 10 A_MonsterLook;
			Loop;
			
		See:
			DEMN ABCD 4 A_MonsterChase;
			Loop;
			
		Parry:
			DEMN E 4;
			DEMN E 31 A_Pain;
			Goto See;
			
		Stumble:
			TNT1 A 0 A_Stumble;
			DEMN E 18 A_Pain;
			Goto See;
			
		Shock:
			DEMN H 9 A_StartSound(PainSound, CHAN_VOICE);
			DEMN H 9;
			DEMN H 9 A_StartSound(PainSound, CHAN_VOICE);
			DEMN H 9;
			DEMN H 9 A_StartSound(PainSound, CHAN_VOICE);
			DEMN H 9;
			DEMN H 9 A_StartSound(PainSound, CHAN_VOICE);
			DEMN H 9;
			Goto See;
			
		Pain:
			DEMN E 4;
			DEMN E 4 A_Pain;
			Goto See;
			
		Melee:
			DEMN E 6 A_MonsterFaceTarget;
			DEMN F 8 A_MonsterFaceTarget;
			DEMN G 6 A_MonsterMelee("SerpentBite", 40, 0, 1, 64);
			Goto See;
			
		Missile:
			DEMN E 5 A_MonsterFaceTarget;
			DEMN F 6 A_MonsterFaceTarget;
			DEMN G 5 A_MonsterProjectile("SerpentRFX1", 62);
			Goto See;
			
		Death:
			DEMN HI 6;
			DEMN J 6 A_Scream;
			DEMN K 6 A_NoBlocking;
			DEMN L 6 A_QueueCorpse;
			DEMN MNO 6;
			DEMN P -1;
			Stop;
			
		XDeath:
			DEMN H 6;
			DEMN I 6
			{
				static const class<Actor> chunks[] = { "Demon1Chunk1", "Demon1Chunk2", "Demon1Chunk3", "Demon1Chunk4", "Demon1Chunk5" };
				for(int i = 0; i < 5; i++)
					A_SpawnItemEx(chunks[i], 0,0,45, frandom[DemonChunks](1,4.984375)*cos(Angle+90), frandom[DemonChunks](1,4.984375)*sin(Angle+90), 8, 90, ChunkFlags);
			}
			Goto Death+2;
			
		Ice:
			DEMN Q 5 A_FreezeDeath;
			DEMN Q 1 A_FreezeDeathChunks;
			Wait;
		
		Raise:
			DEMN ONMLKJIH 6;
			Goto See;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		
		CreateBox("SerpentBody");
		CreateBox("SerpentHead2");
	}
}

class ReforgedSerpent2 : ReforgedSerpent replaces Demon2 {}

class SerpentBody : CollisionBox
{
	Default
	{
		CollisionBox.ForwardOffset -12;
		Radius 24;
		Height 80;
	}
}

class SerpentHead2 : CollisionBox
{
	Default
	{
		CollisionBox.Multi 1.2;
		CollisionBox.ForwardOffset 28;
		CollisionBox.UpOffset 50;
		CollisionBox.Length 12;
		Radius 13;
		Height 31;
		
		+COLLISIONBOX.CRITICALZONE
		+COLLISIONBOX.ORIENTED
	}
}

class SerpentBite : HurtBox
{
	Default
	{
		Radius 2;
		Height 4;
		ActiveSound "FighterPunchHitWall";
		DamageType "Cleave";
		HurtBox.AilmentType "Bleed";
		HurtBox.AilmentPower 50;
		HurtBox.AilmentDamage 10;
		HurtBox.UpOffset 14;
		HurtBox.ForwardOffset 24;
	}
}

class SerpentRFX1 : RPGMissile
{
	Default
	{
		Speed 16;
		Radius 5;
		Height 10;
		Damage 30;
		DamageType "Poison";
		RPGMissile.AilmentType "Poison";
		RPGMissile.AilmentPower 35;
		RPGMissile.AilmentDamage 10;
		RenderStyle "Add";
		SeeSound "DemonMissileFire";
		DeathSound "DemonMissileExplode";
		
		+SPAWNSOUNDSOURCE
		+ZDOOMTRANS
	}
	
	States
	{
		Spawn:
			D2FX ABCDEF 4 Bright;
			Loop;
			
		Death:
			D2FX GHIJ 4 Bright;
			D2FX KL 3 Bright;
			Stop;
	}
}